plugins {
    id "com.google.protobuf" version "0.8.18"
    id "org.jetbrains.kotlin.jvm" version "$version_kotlin"
}

group 'org.vitrivr'
version '0.14.4-SNAPSHOT'

apply plugin: 'com.google.protobuf'
apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'java-library'
apply plugin: 'maven-publish'
apply plugin: 'signing'

repositories {
    mavenCentral()
    maven { url "https://kotlin.bintray.com/kotlinx" }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {

        mavenJava(MavenPublication) {
            groupId = 'org.vitrivr'
            artifactId = 'cottontaildb-proto'
            version = System.getenv().getOrDefault("MAVEN_PUBLICATION_VERSION", version.toString())
            pom {
                name = 'Cottontail DB (Driver)'
                description = 'The gRPC and ProtoBuf definitions (driver) for Cottontail DB.'
                url = 'https://github.com/vitrivr/cottontaildb-proto/'
                licenses {
                    license {
                        name = 'MIT License'
                    }
                }
                developers {
                    developer {
                        id = 'ppanopticon'
                        name = 'Ralph Gasser'
                        email = 'ralph.gasser@unibas.ch'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/vitrivr/cottontaildb-proto.git'
                    url = 'https://github.com/vitrivr/cottontaildb-proto'
                }
            }
            from components.java
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
            def snapshotsRepoUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
            name = "OSSRH"
            url = (publishing.publications.mavenJava.version.endsWith('SNAPSHOT')) ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = System.getenv("MAVEN_USERNAME")
                password = System.getenv("MAVEN_PASSWORD")
            }
        }
    }
}

signing {
    def signingKey = findProperty("signingKey")
    def signingPassword = findProperty("signingPassword")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.mavenJava
}

sourceSets {
    main {
        proto {
            srcDirs += "$projectDir/src/main/protobuf"
        }
        java {
            srcDirs += "$projectDir/build/generated/source/proto/main/java"
        }
        kotlin {
            srcDirs += "$projectDir/build/generated/source/proto/main/kotlin"
        }
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

protobuf {
    generatedFilesBaseDir = "$projectDir/build/generated/source/proto"
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:$version_grpc"
        }
        grpckt {
            artifact = "io.grpc:protoc-gen-grpc-kotlin:$version_grpc_kotlin:jdk7@jar"
        }
    }
    protoc {
        artifact = "com.google.protobuf:protoc:$version_protobuf"
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {
                outputSubDir = 'java'
            }
            grpckt {
                outputSubDir = 'kotlin'
            }
        }
    }
}

dependencies {
    /////// gRPC & Protobuf
    implementation group: 'com.google.protobuf', name: 'protobuf-java', version: version_protobuf
    implementation group: 'io.grpc', name: 'grpc-netty', version: version_grpc
    implementation group: 'io.grpc', name: 'grpc-protobuf', version: version_grpc
    implementation group: 'io.grpc', name: 'grpc-stub', version: version_grpc
    implementation group: 'io.grpc', name: 'grpc-kotlin-stub', version: version_grpc_kotlin
    compileOnly group: 'org.apache.tomcat', name: 'tomcat-annotations-api', version: version_annotations

    /////// Kotlin references
    implementation group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib', version: version_kotlin
    implementation group: 'org.jetbrains.kotlinx', name: 'kotlinx-coroutines-core', version: version_kotlinx_coroutines
}